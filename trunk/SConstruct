# FuDePan boilerplate required here

import os
import sys
import SCons

env = Environment()
Export('env')

# SCons configuration options
# Add parallelism to the build system
from multiprocessing import cpu_count
SetOption('num_jobs', cpu_count() + 1)

# Add variables, this variables can be introduced through a file or
# can be introduced via command line
vars = Variables('SConfig')
# Get the scons root path, this can be tricky because 
# scons can be ran with the -v option
vars.AddVariables(
    PathVariable(
        'WS_DIR',
        'workspace directory',
        env.Dir('#').abspath,
        PathVariable.PathIsDirCreate))
vars.AddVariables(
    PathVariable(
        'BUILD_DIR',
        'build directory',
        os.path.join(env.Dir('#').abspath, "build"),
        PathVariable.PathIsDirCreate))
vars.AddVariables(
    PathVariable(
        'INSTALL_DIR',
        'install directory',
        os.path.join(env.Dir('#').abspath, "install"),
        PathVariable.PathIsDirCreate))
vars.AddVariables(
    PathVariable(
        'BUILD_SCRIPTS_DIR',
        'site_scons directory',
        os.path.join(env.Dir('#').abspath, "site_scons"),
        PathVariable.PathIsDirCreate))
vars.Update(env)

# Add the script paths so is easier to find the py modules
sys.path.append(env['BUILD_SCRIPTS_DIR'])

# Resolve the rpath so everything works smooth from
# the install dir. This makes easier to deploy somewhere
# else, you only need to pass INSTALL_DIR parameter to 
# scons to deploy it somewhere else
env.Append( RPATH = env['INSTALL_DIR'] )
env['ENV']['LD_LIBRARY_PATH'] = env['INSTALL_DIR']

# Some environment tunnings so this runs faster
env.Decider( 'MD5-timestamp' )
env.SConsignFile()

# Linker options
import platform
(arch,binType) = platform.architecture()
if arch == '64bit' and binType == 'ELF':
    env['CXXFLAGS'] = '-fPIC'

# Wrapper functions to build target types
from SCons.Script.SConscript import SConsEnvironment
import component
SConsEnvironment.CreateProgram = component.CreateProgram
SConsEnvironment.CreateStaticLibrary = component.CreateStaticLibrary
SConsEnvironment.CreateSharedLibrary = component.CreateSharedLibrary
SConsEnvironment.CreateHeaderOnlyLibrary = component.CreateHeaderOnlyLibrary
SConsEnvironment.CreateTest = component.CreateTest
SConsEnvironment.AddComponent = component.AddComponent

# Register builders
# Register tools
env.Tool('doxygen')
env.Tool('makebuilder')

# Create a builder for tests
import test
bld = Builder(action = test.runTest)
env.Append(BUILDERS = {'Test' :  bld})

# Add Qt
import qtutil
env.Tool('qt')
qtutil.addQtComponents(env)
import boostutil
boostutil.addBoostComponents(env)

# Add OS Components
import linux
linux.addOSComponents(env)

# Color pretty printing
import termcolor
if ARGUMENTS.get('VERBOSE') != '1':
    termcolor.prettyMessages(env)

# call to fudepan.py where 
import fudepan
fudepan.setDefines(env)

# Walk over the tree finding components
from component import WalkDirsForComponents
WalkDirsForComponents(env, topdir = env['WS_DIR'], 
                      ignore = [
                               'gmock/scons',
                               'buildtest/test_program',
                               'buildtest/test_qt',
                               'buildtest/test_shared',
                               'buildtest/test_static',
                               ])

